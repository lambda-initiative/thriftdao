if (!hasProperty('scalaVersion')) {
    scalaVersion = '2.10.4'
}
def (major, minor, trivial) = scalaVersion.tokenize('.')
scalaLevel = major + "." + minor

if (!hasProperty('scroogeVersion')) {
    scroogeVersion = '3.14.1'
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.github.iamsteveholmes:scrooge-gradle-plugin:0.5"
        classpath "org.scala-lang:scala-library:${scalaVersion}"
        classpath "com.twitter:scrooge-generator_${scalaLevel}:${scroogeVersion}"
    }
}

apply {
    plugin "scrooge-gradle-plugin"
    plugin "scala"
}

repositories {
    mavenCentral()
}

dependencies {
    compile "com.foursquare:common-thrift-bson:2.0.0-M4"
    compile "com.twitter:scrooge-serializer_${scalaLevel}:${scroogeVersion}"
    compile "org.mongodb:casbah_${scalaLevel}:2.7.1"

    testCompile "junit:junit:4.11"
    testCompile "org.scalatest:scalatest_${scalaLevel}:2.1.0"
}

sourceSets.test.scala.srcDirs += ["gen/test/scala"]

// TODO refactor the scrooge generator
compileScrooge {
    thriftFiles = fileTree(dir: "src/test/thrift", include: "**/*.thrift")
    dest = file("gen/test/scala")
}
tasks.compileTestScala.dependsOn(compileScrooge)