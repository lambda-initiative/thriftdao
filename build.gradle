buildscript {
  repositories {
    mavenCentral()
    maven {
      url "http://maven.twttr.com"
    }
  }

  dependencies {
    classpath 'com.yodle.griddle:griddle:1.9'
    classpath "org.scala-lang:scala-library:2.10.6"
    classpath 'org.apache.thrift:libthrift:0.9.2'
  }
}

apply plugin: "scala"

if (!hasProperty('scalaVersion')) {
  ext.scalaVersion = '2.11.11'
}
if (!hasProperty('scala210Version')) {
  ext.scala210Version = '2.10.6'
}
if (!hasProperty('finagleVersion')) {
  ext.finagleVersion = "17.12.0"
}

def (major, minor, trivial) = scalaVersion.tokenize('.')
ext.scalaLevel = major + "." + minor

apply {
  plugin "scala"
  plugin "scrooge"
}

repositories {
  mavenCentral()
}

dependencies {
  scroogeGen "com.twitter:scrooge-generator_${scalaLevel}:${finagleVersion}"

  compile "com.foursquare:common-thrift-bson:2.+"
  compile "com.twitter:scrooge-serializer_${scalaLevel}:${finagleVersion}"
  compile "org.mongodb:casbah_${scalaLevel}:3.1.1"
  compile "org.scala-lang:scala-library:${scalaVersion}"

  testCompile "junit:junit:4.+"
  testCompile "org.scalatest:scalatest_${scalaLevel}:2.+"
}

sourceSets.test.scala.srcDirs += ["gen/test/scala"]

thriftSrcDir = "src/test/thrift"
thriftGenDir = "gen/test/scala"
tasks.compileTestScala.dependsOn(generateInterfaces)
