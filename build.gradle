buildscript {
  if (!hasProperty('scalaVersion')) {
    ext.scalaVersion = '2.10.4'
  }
  def (major, minor, trivial) = scalaVersion.tokenize('.')
  ext.scalaLevel = major + "." + minor

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "com.github.iamsteveholmes:scrooge-gradle-plugin:+"
    classpath "org.scala-lang:scala-library:${scalaVersion}"
    classpath "com.twitter:scrooge-generator_${scalaLevel}:3.+"
  }
}

apply {
  plugin "scrooge-gradle-plugin"
  plugin "scala"
}

repositories {
  mavenCentral()
}

dependencies {
  compile "com.foursquare:common-thrift-bson:2.+"
  compile "com.twitter:scrooge-serializer_${scalaLevel}:3.+"
  compile "org.mongodb:casbah_${scalaLevel}:2.+"

  testCompile "junit:junit:4.+"
  testCompile "org.scalatest:scalatest_${scalaLevel}:2.+"
}

sourceSets.test.scala.srcDirs += ["gen/test/scala"]

compileScrooge {
  thriftFiles = fileTree(dir: "src/test/thrift", include: "**/*.thrift")
  dest = file("gen/test/scala")
}
tasks.compileTestScala.dependsOn(compileScrooge)
